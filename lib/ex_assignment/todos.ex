defmodule ExAssignment.Todos do
  @moduledoc """
  Provides operations for working with todos.
  """

  import Ecto.Query, warn: false
  alias ExAssignment.Repo

  alias ExAssignment.Todos.Todo
  alias ExAssignment.RecommendationServer, as: Cache

  @doc """
  Returns the list of todos, optionally filtered by the given type.

  ## Examples

      iex> list_todos(:open)
      [%Todo{}, ...]

      iex> list_todos(:done)
      [%Todo{}, ...]

      iex> list_todos()
      [%Todo{}, ...]

  """
  def list_todos(type \\ nil) do
    cond do
      type == :open ->
        from(t in Todo, where: not t.done, order_by: t.priority)
        |> Repo.all()

      type == :done ->
        from(t in Todo, where: t.done, order_by: t.priority)
        |> Repo.all()

      true ->
        from(t in Todo, order_by: t.priority)
        |> Repo.all()
    end
  end

  @doc """
  Returns the next todo that is recommended to be done by the system.

  Cache a generated recommendation and invalidate after it is marked as complete.

  ASSIGNMENT: ...
  """

  def get_recommended() do
    Cache.get_recommendation() |> get_recommended()
  end

  def get_recommended(todo) when is_nil(todo) do
    list_todos(:open)
    |> case do
      [] ->
        nil

      todos ->
        recommendation = Enum.take_random(todos, 1) |> List.first()

        :ok = Cache.set_recommendation(recommendation)
        recommendation
    end
  end

  def get_recommended(todo), do: todo

  @doc """
  Gets a single todo.

  Raises `Ecto.NoResultsError` if the Todo does not exist.

  ## Examples

      iex> get_todo!(123)
      %Todo{}

      iex> get_todo!(456)
      ** (Ecto.NoResultsError)

  """
  def get_todo!(id), do: Repo.get!(Todo, id)

  @doc """
  Creates a todo.

  ## Examples

      iex> create_todo(%{field: value})
      {:ok, %Todo{}}

      iex> create_todo(%{field: bad_value})
      {:error, %Ecto.Changeset{}}

  """
  def create_todo(attrs \\ %{}) do
    %Todo{}
    |> Todo.changeset(attrs)
    |> Repo.insert()
  end

  @doc """
  Updates a todo.

  ## Examples

      iex> update_todo(todo, %{field: new_value})
      {:ok, %Todo{}}

      iex> update_todo(todo, %{field: bad_value})
      {:error, %Ecto.Changeset{}}

  """
  def update_todo(%Todo{} = todo, attrs) do
    todo
    |> Todo.changeset(attrs)
    |> Repo.update()
  end

  @doc """
  Deletes a todo.

  ## Examples

      iex> delete_todo(todo)
      {:ok, %Todo{}}

      iex> delete_todo(todo)
      {:error, %Ecto.Changeset{}}

  """
  def delete_todo(%Todo{} = todo) do
    response = Repo.delete(todo)

    cached = Cache.get_recommendation()

    if present?(cached, todo) do
      Cache.invalidate_recommendation()
    end

    response
  end

  @doc """
  Returns an `%Ecto.Changeset{}` for tracking todo changes.

  ## Examples

      iex> change_todo(todo)
      %Ecto.Changeset{data: %Todo{}}

  """
  def change_todo(%Todo{} = todo, attrs \\ %{}) do
    Todo.changeset(todo, attrs)
  end

  @doc """
  Marks the todo referenced by the given id as checked (done).

  ## Examples

      iex> check(1)
      :ok

  """
  def check(id) do
    get_todo!(id)
    |> update_todo(%{done: true})
    |> case do
      {:ok, todo} ->
        cached = Cache.get_recommendation()

        if present?(cached, todo) do
          Cache.invalidate_recommendation()
        end

        :ok

      e ->
        e
    end
  end

  @doc """
  Marks the todo referenced by the given id as unchecked (not done).

  ## Examples

      iex> uncheck(1)
      :ok

  """
  def uncheck(id) do
    get_todo!(id)
    |> update_todo(%{done: false})
    |> case do
      {:ok, _} ->
        :ok

      e ->
        e
    end
  end

  defp present?(nil, _todo), do: false

  defp present?(cached_todo, todo) do
    cached_todo.id == todo.id
  end
end
